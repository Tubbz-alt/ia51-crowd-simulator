/** 
 * 
 */
package io.sarl.fimu.environment

import io.sarl.fimu.environment.frustum.IFrustum
import java.util.ArrayList
import java.util.List
import java.util.UUID
import javafx.scene.paint.Color
import org.arakhne.afc.math.MathUtil
import org.arakhne.afc.math.geometry.d2.d.Shape2d
import org.arakhne.afc.math.geometry.d2.d.Vector2d

/** 
 * @author LÃ©on
 * 
 */
class AgentBody extends AbstractMobileObject {
	
	val frustum : IFrustum

	var motionInfluence : MotionInfluence
	var otherInfluences : List<Influence> = new ArrayList
	var perceptions : List<Percept> = new ArrayList
	var color : Color

	new (id : UUID, shape : Shape2d<?>, maxLinearSpeed : double,  maxLinearAcceleration : double, maxAngularSpeed : double,
		maxAngularAcceleration : double, frustum : IFrustum, color : Color = null) {
		super(id, shape, maxLinearSpeed, maxLinearAcceleration, maxAngularSpeed, maxAngularAcceleration)
		//TODO assert (frustum == null || Objects.equals(id, frustum.getOwner()));
		this.frustum = frustum
		this.type = "BODY"
		this.color = color
		if(color === null){
			this.color = Color.YELLOW
		}
	}
	
	def getColor { color }
	def setColor(c : Color ){ color = c}
	
	def getFrustum : IFrustum {
		this.frustum
	}

	def consumeMotionInfluence : MotionInfluence {
		var mi = this.motionInfluence
		this.motionInfluence = null
		mi?.setEmitter(id)
		mi
	}

	package def consumeOtherInfluences : List<Influence> {
		var otherInfluences = this.otherInfluences
		this.otherInfluences = new ArrayList
		for (i : otherInfluences) {
			i?.setEmitter(id)
		}
		otherInfluences
	}
	
	/*
	 * Send an influence to the environment
	 */
	def influence(influence : Influence) { 
		if (influence instanceof MotionInfluence) {
			influenceKinematic(influence.linearInfluence, influence.angularInfluence)
		} else if (influence !== null) {
			this.otherInfluences += influence
		}
	}

	def setPerceptions(perceptions : List<Percept>) : void {
		// TODO assert(perceptions!=null);
		this.perceptions = perceptions
	}
	
	def influenceKinematic(linearInfluence : Vector2d = null, angularInfluence : double = 0.0) {
		var li : Vector2d
		if (linearInfluence !== null) {
			li = linearInfluence.clone
			var nSpeed = li.length
			if (nSpeed > this.maxLinearSpeed) {
				li.length = this.maxLinearSpeed
			}
		}
		var ai = MathUtil::clamp(angularInfluence, -this.maxAngularSpeed, this.maxAngularSpeed)
		this.motionInfluence = new MotionInfluence(id, li, ai)
	}

	def getPerceivedObjects : List<Percept> {
		this.perceptions
	}
}
