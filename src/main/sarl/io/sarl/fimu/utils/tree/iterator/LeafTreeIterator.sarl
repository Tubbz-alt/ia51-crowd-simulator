/* 
 * $Id$
 * 
 * Copyright (c) 2011-17 Stephane GALLAND <stephane.galland@utbm.fr>.
 * 
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 * 
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 * This program is free software; you can redistribute it and/or modify
 */
package io.sarl.fimu.utils.tree.iterator

import io.sarl.fimu.utils.tree.SpatialTreeNode
import java.util.Iterator
import java.util.NoSuchElementException
import java.util.Stack

/**
 * Iterator on tree leaves.
 * 
 * @param <N> - type of the root node.
 * @author St&eacute;phane GALLAND &lt;stephane.galland@utbm.fr&gt;
 * @version $Name$ $Revision$ $Date$
 */
class LeafTreeIterator<N extends SpatialTreeNode<N, ?>> implements Iterator<N> {

	val stack = new Stack<N>
	var next : N
	
	/**
	 * @param root - the root node.
	 */
	new (root : N) {
		if (root !== null) {
			this.stack += root
		}
		searchNext
	}
	
	private def searchNext {
		this.next = null
		while (this.next === null && !this.stack.isEmpty) {
			var candidate = this.stack.pop
			if (candidate.leaf) {
				this.next = candidate
			} else {
				for (child : candidate.children) {
					this.stack += child
				}
			}
		}
	}
	
	override hasNext : boolean {
		this.next !== null
	}

	override next : N {
		if (this.next === null) {
			throw new NoSuchElementException
		}
		var n = this.next
		searchNext
		return n
	}

}
