/* 
 * $Id$
 * 
 * Copyright (c) 2011-17 Stephane GALLAND <stephane.galland@utbm.fr>.
 * 
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 * 
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 * This program is free software; you can redistribute it and/or modify
 */
package io.sarl.fimu.utils.tree.iterator

import io.sarl.fimu.environment.IShapedObject
import io.sarl.fimu.utils.tree.SpatialTreeNode
import java.util.Iterator
import java.util.LinkedList
import java.util.NoSuchElementException

/**
 * Depth-first tree iterator.
 * 
 * @param <N> - type of the root node.
 * @param <D> - type of the data in the nodes.
 * @author St&eacute;phane GALLAND &lt;stephane.galland@utbm.fr&gt;
 * @version $Name$ $Revision$ $Date$
 */
class DepthFirstObjectTreeIterator<N extends SpatialTreeNode<N, D>, D extends IShapedObject> implements Iterator<D> {

	val iterator : DepthFirstTreeIterator<N>
	val nexts = new LinkedList<D>
	
	/**
	 * @param root - the root node.
	 */
	new (root : N) {
		this.iterator = new DepthFirstTreeIterator(root)
		searchNext
	}
	
	private def searchNext {
		if (this.nexts.isEmpty) {
			while (this.iterator.hasNext && this.nexts.empty) {
				var n = this.iterator.next
				this.nexts += n.data
			}
		}
	}
	
	override hasNext : boolean {
		!this.nexts.empty
	}

	override next : D {
		if (this.nexts.empty) {
			throw new NoSuchElementException
		}
		var n = this.nexts.removeFirst
		searchNext
		return n
	}

}
